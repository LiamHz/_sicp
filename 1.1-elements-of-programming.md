SICP - Section 1.1 Solutions
============================
**Note:** Original question text is in _italics_

**Note:** If you open this file in vim, you can visually select code blocks and run them with `:term scheme`

## Table of Contents
* [Exercise 1.1: Evaluating Expressions](#exercise-11-evaluating-expressions)
* [Exercise 1.2: Prefix Form Practice](#exercise-12-prefix-form-practice)
* [Exercise 1.3: Sum of Largest Squares](#exercise-13-sum-of-largest-squares)
* [Exercise 1.4: Operators from Compound Expressions](#exercise-14-operators-from-compound-expressions)
* [Exercise 1.5: Applictive-Order and Normal-Order Evaluation](#exercise-15-applictive-order-and-normal-order-evaluation)
* [Exercise 1.6: The Special form of if](#exercise-16-the-special-form-of-if)
* [Exercise 1.7: Improving The Iterative Square Root Procedure](#exercise-17-improving-the-iterative-square-root-procedure)
* [Exercise 1.8: Newton's Method for Cube Roots](#exercise-18-newtons-method-for-cube-roots)

## Exercise 1.1: Evaluating Expressions
_Below is a sequence of expressions. What is the result printed by the interpreter in response to each expression? Assume that the sequence is to be evaluated in the order in which it is presented._
```lisp
10                                  ; 10
(+ 5 3 4)                           ; 12
(- 9 1)                             ; 8
(/ 6 2)                             ; 3
(+ (* 2 4) (- 4 6))                 ; 6
(define a 3)                         
(define b (+ a 1))                   
(+ a b (* a b))                     ; 19
(= a b)                             ; false
(if (and (> b a) (< b (* a b)))      
    b                                 
    a)                              ; 4
(cond ((= a 4) 6)                    
      ((= b 4) (+ 6 7 a))            
      (else 25))                    ; 16
(+ 2 (if (> b a) b a))              ; 6
(* (cond ((> a b) a)                 
         ((< a b) b)                 
         (else -1))                  
   (+ a 1))                         ; 16
```

## Exercise 1.2: Prefix Form Practice
_Translate the following expression into prefix form:_

**( 5 + 4 + (2 − (3 − (6 + 4/5))) ) / ( 3(6 − 2)(2 − 7) )**
```lisp
(/ (+ 5 4 (- 2 (- 3 (+ 6 (/ 4 5))))) (* 3 (- 6 2) (- 2 7)))
```

**Observation:** To convert from infix to prefix notation, use reverse BEDMAS

## Exercise 1.3: Sum of Largest Squares
_Define a procedure that takes three numbers as arguments and returns the sum of the squares of the two larger numbers._
```lisp
(define (sum-of-largest-squares a b c)
    (cond ((and (< c a) (< c b)) (+ (* a a) (* b b)))
          ((and (< b a) (< b c)) (+ (* a a) (* c c)))
          (else (+ (* b b) (*c c)))))

; Both expressions evaluate to 106
(sum-of-largest-squares 5 9 2)
(sum-of-largest-squares 5 2 9)
```

## Exercise 1.4: Operators from Compound Expressions
_Observe that our model of evaluation allows for combinations whose operators are compound expressions. Use this observation to describe the behavior of the following procedure:_
```lisp
(define (a-plus-abs-b a b)
  ((if (> b 0) + -) a b))

(a-plus-abs-b 5 2)
(a-plus-abs-b 5 -2)
```
1. Defines a function that takes two arguments: `a` and `b`
2. If `b` is greater than 0, return the `+` operator, else return the `-` operator
3. Use the operator returned from step two to operate on `a` and `b`

## Exercise 1.5: Applictive-Order and Normal-Order Evaluation
_Ben Bitdiddle has invented a test to determine whether the interpreter he is faced with is using applicative-order evaluation or normal-order evaluation. He defines the following two procedures. What behavior will he observe after evaluation `(test 0 p)` with an interpreter that uses applicative-order evaluation? With normal-order evaluation?_
```lisp
(define (p) (p))

(define (test x y) 
  (if (= x 0) 0 y))

(test 0 p)  ; Evaluates to 0
```
**Normal-order evaluation:** `(test 0 p)` gets expanded to `(if (= 0 0) 0 p)` and when the conditional gets evaluated as true it returns 0 immeediately

**Applicative-order evaluation:** Arguments are expanded first, and expanding `p` will cause an error.

## Exercise 1.6: The Special form of if
_Alyssa P. Hacker doesn’t see why if needs to be provided as a special form. “Why can’t I just define it as an ordinary procedure in terms of cond?” she asks. Alyssa’s friend Eva Lu Ator claims this can indeed be done, and she defines a new version of if:_
```lisp
(define (new-if predicate 
                then-clause 
                else-clause)
  (cond (predicate then-clause)
        (else else-clause)))
```
_What happens when Alyssa attempts to use the following to compute square roots?_
```lisp
(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x) x)))
```
Because of applicative-order evaluation, all the expressions in the `new-if` function that uses `cond` will be evaluate. This causes an infinite evaluation loop because of sqrt-iter's recursive nature.

The special form `if` only evaluates the alternative expression if the predicate doesn't evaluate to true.

## Exercise 1.7: Improving The Iterative Square Root Procedure
_The good-enough? test used in computing square roots will not be very effective for finding the square roots of very small numbers. Also, in real computers, arithmetic operations are almost always performed with limited precision. This makes our test inadequate for very large numbers. Explain these statements, with examples showing how the test fails for small and large numbers._
```lisp
; Original sqrt-iter function
(define (average x y) 
  (/ (+ x y) 2))

(define (improve guess x)
  (average guess (/ x guess)))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x)))

(define (sqrt x)
  (sqrt-iter 1.0 x))

; Showing the sqrt-iter fails on small and large numbers

; Small number check
; good-enough? will return true for any difference less than 0.001
(good-enough? 0.0005 0.0001) ; Returns true

; Checking for large number failure cases
(sqrt (expt 10 12))       ; This evaluation completes
; (sqrt (expt 10 13))     ; This evaluation doesn't complete

; An alternative strategy for implementing good-enough? is to watch how guess
; changes from one iteration to the next and to stop when the change is a very
; small fraction of the guess. Design a square-root procedure that uses this
; kind of end test. Does this work better for small and large numbers?

; Returns true if change in guess is less than 1% of previous guess
(define (new-good-enough? guess prev-guess)
    (< (abs (- guess prev-guess)) (/ guess 100)))

(define (new-sqrt-iter guess prev-guess x)
    (if (new-good-enough? guess prev-guess)
        guess
        (new-sqrt-iter (improve guess x) guess x)))

(define (new-sqrt x)
    (new-sqrt-iter 1.0 0 x))
(new-sqrt 82)

; Small number check
(new-sqrt 0.005)        ; Returns ~0.0707

; Large number check
(new-sqrt (expt 10 12)) ; This evaluation completes
(new-sqrt (expt 10 13)) ; This evaluation also completes
```

## Exercise 1.8: Newton's Method for Cube Roots 
_Newton’s method for cube roots is based on the fact that if y is an approximation to the cube root of x, then a better approximation is given by an expression [0]. Use this formula to implement a cube-root procedure analogous to the square-root procedure_

Let y be an approximation of the cube root of x

[0] **x^(1/3) = (x / y^2 + 2y) / 3**
```lisp
(define (good-enough? guess prev-guess)
    (< (abs (- guess prev-guess)) (/ guess 100)))

(define (improve guess x)
    (/ (+ (/ x (* guess guess)) (* 2 guess)) 3))

(define (cbrt-iter guess prev-guess x)
    (if (good-enough? guess prev-guess)
        guess
        (cbrt-iter (improve guess x) guess x)))

(define (cbrt x)
    (cbrt-iter 1.0 0 x))

(cbrt 0.0005)       ; Returns ~0.079
(cbrt 512)          ; Returns ~8.00 
(cbrt (expt 10 13)) ; Returns ~21545.34
```
